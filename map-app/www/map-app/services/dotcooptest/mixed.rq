PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX essglobal: <https://w3id.solidarityeconomy.coop/essglobal/V2a/vocab/>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ospostcode: <http://data.ordnancesurvey.co.uk/id/postcodeunit/>
PREFIX rov: <http://www.w3.org/ns/regorg#>
PREFIX osspatialrelations: <http://data.ordnancesurvey.co.uk/ontology/spatialrelations/>
PREFIX : <https://w3id.solidarityeconomy.coop/dotcooptest/>



SELECT ?name ?uri ?within ?lat ?lng ?www ?tel ?email ?regorg ?sameas ?desc ?primaryActivity ?street ?locality ?postcode
FROM <https://w3id.solidarityeconomy.coop/dotcooptest/>
FROM <https://w3id.solidarityeconomy.coop/coops-uk/test2019/>
WHERE {

{
GRAPH <https://w3id.solidarityeconomy.coop/coops-uk/test2019/> {
?uri rdf:type essglobal:SSEInitiative .
?uri owl:sameAs ?sameas.
?uri gr:name ?name .
OPTIONAL { ?uri dc:description ?desc . }
OPTIONAL { ?uri rov:hasRegisteredOrganization ?regorg . }
}
GRAPH <https://w3id.solidarityeconomy.coop/dotcooptest/> {
?sameas rdf:type essglobal:SSEInitiative .
OPTIONAL { ?sameas foaf:homepage ?www . }

?sameas essglobal:hasAddress ?addr .
OPTIONAL { ?addr vcard:street-address ?street . }
OPTIONAL { ?addr vcard:postal-code ?postcode . }
OPTIONAL { ?addr vcard:locality ?locality . }
?addr osspatialrelations:within ?within .
?within geo:lat ?lat.
?within geo:long ?lng.

   }
}
}